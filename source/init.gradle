allprojects {
    def map = [
      "https://repo1.maven.org/maven2/": "https://maven.aliyun.com/repository/central", //central
      "https://repo.maven.apache.org/maven2/": "https://maven.aliyun.com/repository/central", //central
      "http://jcenter.bintray.com/": "https://maven.aliyun.com/repository/public",      // jcentral

      "https://maven.google.com/": "https://maven.aliyun.com/repository/google",        // google
      "https://dl.google.com/dl/android/maven2": "https://dl.google.com/dl/android/maven2", // google                        
      
      "https://plugins.gradle.org/m2/": "https://maven.aliyun.com/repository/gradle-plugin", //  gradle plugin
      
      "https://repo.spring.io/libs-milestone/": "https://maven.aliyun.com/repository/spring", // spring
      "https://repo.spring.io/plugins-release/": "https://maven.aliyun.com/repository/spring-plugin", // spring plugin
      
      "https://repo.grails.org/grails/core": "https://maven.aliyun.com/repository/grails-core", // grails-core
      
      "https://repository.apache.org/snapshots/": "https://maven.aliyun.com/repository/apache-snapshots", // apache snapshots
    ]

    repositories {
        all {
            ArtifactRepository repo ->
            if (repo instanceof MavenArtifactRepository) {
                def url = repo.url.toString()
                map.each { k, v ->
                    if (url.startsWith(k)) {
                        project.logger.lifecycle "Repository ${repo.url} replaced by ${v}."
                        repo.setUrl(v)
                    }
                }
            }
        }
    }

    buildscript {
        repositories {
            maven { url 'https://maven.aliyun.com/repository/public/' }
            all { ArtifactRepository repo ->
                if (repo instanceof MavenArtifactRepository) {
                    def url = repo.url.toString().toString()
                    map.each { k, v ->
                        if (url.startsWith(k)) {
                            project.logger.lifecycle "Repository ${repo.url} replaced by ${v}."
                            repo.setUrl(v)
                        }
                    }
                }
            }
        }
    }
}